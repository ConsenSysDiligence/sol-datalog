// Helper relation to establish a relation is BOTH modified and read.
// Includes:
//      - +=, -= ...
//      - ++/--
//      - calls to .push and .pop (the length must first be read to be incremented/decremented)
.decl LRValue(id: ExpressionId, in: ExpressionId)

// === Top-level cases:
// Assignment lhs
LRValue(x, aId) :-
    Assignment_leftHandSide(aId, x),
    Assignment_operator(aId, op),
    (match(".=", op);
     match("..=", op)).

LRValue(x, uop) :-
    (UnaryOperation_operator(uop, "++");
    UnaryOperation_operator(uop, "--")),
    UnaryOperation_subExpression(uop, x).

LRValue(x, call) :-
    FunctionCall_expression(call, callee),
    (MemberAccess_memberName(callee, "push");
    MemberAccess_memberName(callee, "pop")),
    MemberAccess_expression(callee, x).
    
// === Inductive cases
LRValue(x, z) :-
    MemberAccess_expression(y, x),
    LRValue(y, z).

LRValue(x, z) :-
    IndexAccess_baseExpression(y, x),
    LRValue(y, z).

LRValue(x, z) :-
    TupleExpression_components(y, x, _, _),
    LRValue(y, z).