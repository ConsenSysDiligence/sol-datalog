.comp Read {
    /// ============== EXPRESSIONS ============================
    /// Specifies whether the variable vId may be read while evaluating the
    /// expression eId
    .decl readExpr(eId: ExpressionId, vId: VariableDeclarationId)

    /// ============== readAssignemtnLHS helper ============================
    /// Helper relation for whether a variable id is read on the LHS of an assignment
    .decl readAssignmentLhs(eId: ExpressionId, vId: VariableDeclarationId)

    /// In `a[E] =...` stuff in E is read
    readAssignmentLhs(eId, vId) :-
        IndexAccess_indexExpression(eId, ieId, 1), readExpr(ieId, vId).

    /// In `foo(args) = ...` stuff in args is read
    readAssignmentLhs(eId, vId) :-
        FunctionCall_arguments(eId, argId, _),
        readExpr(argId, vId).

    /// Recursive case for member access
    readAssignmentLhs(eId, vId) :-
        MemberAccess_expression(eId, subExpressionId), readAssignmentLhs(subExpressionId, vId).

    /// Recursive case for tuples
    readAssignmentLhs(eId, vId) :-
        TupleExpression_components(eId, compId, _, _), readAssignmentLhs(compId, vId).

    /// ============== readExpr ============================

    readExpr(eId, vId) :-
        Assignment_rightHandSide(eId, rhsId), readExpr(rhsId, vId).

    readExpr(eId, vId) :-
        Assignment_leftHandSide(eId, rhsId), readAssignmentLhs(rhsId, vId).

    readExpr(eId, vId) :-
        BinaryOperation_leftExpression(eId, lId), readExpr(lId, vId).

    readExpr(eId, vId) :-
        BinaryOperation_rightExpression(eId, rId), readExpr(rId, vId).

    readExpr(eId, vId) :-
        Identifier_referencedDeclaration(eId, vId), VariableDeclaration(vId).

    readExpr(eId, vId) :-
        FunctionCall_expression(eId, calleeId), readExpr(calleeId, vId).

    readExpr(eId, vId) :-
        FunctionCall_arguments(eId, argId, _), readExpr(argId, vId).

    readExpr(eId, vId) :-
        Conditional_condition(eId, cId), readExpr(cId, vId).

    readExpr(eId, vId) :-
        Conditional_falseExpression(eId, fId), readExpr(fId, vId).

    readExpr(eId, vId) :-
        Conditional_trueExpression(eId, tId), readExpr(tId, vId).

    readExpr(eId, vId) :-
        FunctionCallOptions_expression(eId, subId), readExpr(subId, vId).

    readExpr(eId, vId) :-
        FunctionCallOptions_options(eId, _, optId), readExpr(optId, vId).

    readExpr(eId, vId) :-
        UnaryOperation_subExpression(eId, subId),
        UnaryOperation_operator(eId, operator), operator != "delete",
        readExpr(subId, vId).

    readExpr(eId, vId) :-
        UnaryOperation_subExpression(eId, subId),
        UnaryOperation_operator(eId, operator), operator = "delete",
        readAssignmentLhs(subId, vId).

    readExpr(eId, vId) :-
        IndexAccess_baseExpression(eId, bId), readExpr(bId, vId).

    readExpr(eId, vId) :-
        IndexAccess_indexExpression(eId, iId, 1), readExpr(iId, vId).

    readExpr(eId, vId) :-
        MemberAccess_expression(eId, baseId), readExpr(baseId, vId).

    readExpr(eId, vId) :-
        MemberAccess_referencedDeclaration(eId, vId), VariableDeclaration(vId).

    readExpr(eId, vId) :-
        TupleExpression_components(eId, compId, _, _), readExpr(compId, vId).

    readExpr(eId, vId) :-
        IndexRangeAccess_baseExpression(eId, baseId), readExpr(baseId, vId).

    readExpr(eId, vId) :-
        IndexRangeAccess_startExpression(eId, sId, 1), readExpr(sId, vId).

    readExpr(eId, vId) :-
        IndexRangeAccess_endExpression(eId, eId, 1), readExpr(sId, vId).

    readStmt(sId, vId) :-
        ExpressionStatement_expression(sId, eId), readExpr(eId, vId).

    /// ============== STATEMENTS ============================
    /// Specifies whether the variable vId may be read while evaluating the
    /// statement sId
    .decl readStmt(sId: StatementId, vId: VariableDeclarationId)

    readStmt(sId, vId) :-
        ForStatement_initializationExpression(sId, subId, 1), readExpr(subId, vId).

    readStmt(sId, vId) :-
        ForStatement_condition(sId, subId, 1), readExpr(subId, vId).

    readStmt(sId, vId) :-
        ForStatement_loopExpression(sId, subId, 1), readStmt(subId, vId).

    readStmt(sId, vId) :-
        ForStatement_body(sId, bodyId), readStmt(bodyId, vId).

    readStmt(sId, vId) :-
        TryStatement_externalCall(sId, callId), readExpr(callId, vId).

    readStmt(sId, vId) :-
        TryStatement_clauses(sId, clauseId, _), readStmt(clauseId, vId).

    readStmt(sId, vId) :-
        EmitStatement_eventCall(sId, callId), readExpr(callId, vId).

    /// TODO: Do inline assembly!!
    readStmt(sId, vId) :-
        Block_statements(sId, subId, _), readStmt(subId, vId).

    readStmt(sId, vId) :-
        RevertStatement_errorCall(sId, callId), readExpr(callid, vId).

    readStmt(sId, vId) :-
        UncheckedBlock_statements(sId, subId, _), readStmt(subId, vId).

    readStmt(sId, vId) :-
        Return_expression(sId, exprId, 1), readExpr(exprId, vId).

    readStmt(sId, vId) :-
        WhileStatement_condition(sId, condId), readExpr(condId, vId).

    readStmt(sId, vId) :-
        WhileStatement_body(sId, bodyId), readStmt(bodyId, vId).

    readStmt(sId, vId) :-
        VariableDeclarationStatement_initialValue(sId, exprId, 1), readExpr(exprId, vId).

    readStmt(sId, vId) :-
        IfStatement_condition(sId, cId), readExpr(cId, vId).

    readStmt(sId, vId) :-
        IfStatement_trueBody(sId, tId), readStmt(tId, vId).

    readStmt(sId, vId) :-
        IfStatement_falseBody(sId, fId, 1), readStmt(fId, vId).

    readStmt(sId, vId) :-
        TryCatchClause_block(sId, bId), readStmt(bId, vId).

    readStmt(sId, vId) :-
        DoWhileStatement_condition(sId, cId), readExpr(cId, vId).

    readStmt(sId, vId) :-
        DoWhileStatement_body(sId, bId), readStmt(bId, vId).

    /// ============== Function/Modifiers ============================
    .decl readModifier(mId: ModifierDefinitionId, vId: VariableDeclarationId)
    readModifier(mId, vId) :-
        ModifierDefinition_body(mId, bodyId, 1), readStmt(bodyId, vId).

    .decl readFunction(fId: FunctionDefinitionId, vId: VariableDeclarationId)
    readFunction(fId, vId) :-
        hasBody(fId, bodyId), readStmt(bodyId, vId).
        
    readFunction(fId, vId) :-
        FunctionDefinition_modifiers(fId, mId, _), ModifierInvocation_arguments(mId, eId, _), readExpr(eId, vId).
}