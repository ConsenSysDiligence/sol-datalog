.comp Read {
    .decl readsVar(id: ExpressionId, vId: VariableDeclarationId)
    readsVar(id, vId) :-
        Identifier_referencedDeclaration(id, vId),
        VariableDeclaration(vId),
        !LValue(id).

    readsVar(id, vId) :-
        MemberAccess_referencedDeclaration(id, vId),
        VariableDeclaration(vId),
        !StructDefinition_members(_, vId, _),
        !LValue(id).

    /// ============== Functions ============================
    .decl readsFunction(fId: FunctionDefinitionId, vId: VariableDeclarationId, locId: id)
    readsFunction(fId, vId, locId) :-
        FunctionDefinition(fId),
        ancestor(fId, locId),
        readsVar(locId, vId).
        
    readsFunction(fId, vId, locId) :-
        FunctionDefinition_modifiers(fId, mId, _),
        ModifierInvocation_vReferencedDeclaration(mId, modDefId),
        ancestor(modDefId, locId),
        readsVar(locId, vId).
}